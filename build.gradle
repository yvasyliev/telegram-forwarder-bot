import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    id 'org.springframework.boot' version '3.5.4' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    group = 'io.github.yvasyliev.telegramforwarder'
    version = '4.1.0'

    repositories {
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }

    plugins.withType(JavaLibraryPlugin).configureEach {
        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs.add("-parameters")
        }
    }

    plugins.withType(JavaPlugin).configureEach {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(21)
            }
        }

        checkstyle {
            ignoreFailures = false
            maxWarnings = 0
//            sourceSets = [project.sourceSets.main]
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
            mockitoAgent
        }

        dependencies {
            implementation 'org.springframework.boot:spring-boot-starter'
            annotationProcessor 'org.projectlombok:lombok'
            testImplementation 'org.springframework.boot:spring-boot-starter-test'
            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
            mockitoAgent('org.mockito:mockito-core') {
                transitive = false
            }
            checkstyle 'com.puppycrawl.tools:checkstyle:11.0.0'
            checkstyle 'com.github.sevntu-checkstyle:sevntu-checks:1.44.1'

            test {
                useJUnitPlatform()
                jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
                finalizedBy jacocoTestReport
            }
        }

        jacocoTestReport {
            dependsOn test
            reports {
                xml.required = true
            }
        }
    }
}
