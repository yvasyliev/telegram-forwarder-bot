import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
    alias libs.plugins.freefair.aggregate.javadoc

    id 'org.sonarqube' version '7.2.2.6593'

    alias libs.plugins.freefair.lombok apply false

    id 'org.springframework.boot' version '4.0.2' apply false
    id 'io.spring.dependency-management' version '1.1.7' apply false
}

allprojects {
    apply plugin: 'io.spring.dependency-management'

    group = 'io.github.yvasyliev.forwarder.telegram'
    version = '5.0.2'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom SpringBootPlugin.BOM_COORDINATES
        }
    }
}

subprojects {
    apply plugin: libs.plugins.freefair.lombok.get().pluginId

    apply plugin: 'jacoco'
    apply plugin: 'checkstyle'

    plugins.withType(JavaLibraryPlugin).configureEach {
        tasks.withType(JavaCompile).configureEach {
            options.compilerArgs.add("-parameters")
        }
    }

    plugins.withType(JavaPlugin).configureEach {
        java {
            toolchain {
                languageVersion = JavaLanguageVersion.of(25)
            }
        }

        configurations {
            compileOnly {
                extendsFrom annotationProcessor
            }
            mockitoAgent
        }

        dependencies {
            testImplementation 'org.springframework.boot:spring-boot-starter-test'

            testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

            mockitoAgent('org.mockito:mockito-core') {
                transitive = false
            }

            checkstyle 'com.puppycrawl.tools:checkstyle:13.0.0'
            checkstyle 'com.github.sevntu-checkstyle:sevntu-checks:1.44.1'
        }

        test {
            useJUnitPlatform()
            jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
            finalizedBy jacocoTestReport
        }

        jacocoTestReport {
            dependsOn test
            reports {
                xml.required = true
            }
        }

        checkstyle {
            ignoreFailures = false
            maxWarnings = 0
        }
    }
}

dependencies {
    subprojects.each {
        javadoc it
    }
}

sonar {
    properties {
        property "sonar.projectKey", "io.github.yvasyliev.forwarder.telegram:telegram-forwarder-bot"
        property "sonar.organization", "yvasyliev"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}
