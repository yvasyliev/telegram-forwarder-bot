import org.springframework.boot.gradle.plugin.SpringBootPlugin

plugins {
//    id 'java-library'
    id 'org.springframework.boot' apply false
//    id 'io.spring.dependency-management'
    id 'jacoco'
}

//java {
//    toolchain {
//        languageVersion = JavaLanguageVersion.of(21)
//    }
//}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    mockitoAgent
}

dependencyManagement {
    imports {
        mavenBom SpringBootPlugin.BOM_COORDINATES
    }
}

dependencies {
    implementation libs.bundles.telegrambots
    implementation project(':telegram-forwarder-starter')
    implementation 'org.apache.commons:commons-collections4:4.5.0'
    implementation 'org.jsoup:jsoup:1.21.1'
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    mockitoAgent('org.mockito:mockito-core') {
        transitive = false
    }
}

test {
    useJUnitPlatform()
    jvmArgs += "-javaagent:${configurations.mockitoAgent.asPath}"
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
    }
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                'io/github/yvasyliev/telegramforwarder/reddit/configuration/**',
                'io/github/yvasyliev/telegramforwarder/reddit/dto/**'
        ])
    }))
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add("-parameters")
}
