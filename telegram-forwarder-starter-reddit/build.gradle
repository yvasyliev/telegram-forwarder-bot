plugins {
    id 'java-library'
    id 'org.springframework.boot' apply false
}

dependencies {
    implementation project(':telegram-forwarder-starter')

    implementation libs.bundles.telegrambots
    implementation libs.commons.collections4
    implementation libs.mapstruct

    implementation 'org.jsoup:jsoup:1.21.1'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.boot:spring-boot-starter-restclient'
    implementation 'org.springframework.boot:spring-boot-starter-validation'

    annotationProcessor libs.mapstruct.processor
}

jacocoTestReport {
    classDirectories.setFrom(files(classDirectories.files.collect {
        fileTree(dir: it, exclude: [
                'io/github/yvasyliev/telegramforwarder/reddit/configuration/**',
                'io/github/yvasyliev/telegramforwarder/reddit/entity/**',
                'io/github/yvasyliev/telegramforwarder/reddit/dto/**'
        ])
    }))
}

compileJava {
    options.compilerArgs += [
            '-Amapstruct.defaultComponentModel=spring',
            '-Amapstruct.defaultInjectionStrategy=constructor',
    ]
}
